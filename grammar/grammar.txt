TODO: Added comments

block           : LBRACE NEWLINE* statements? NEWLINE* RBRACE

statements      : expr (NEWLINE+ expr)*

expr            : KEYWORD:sea IDENTIFIER EQ expr
                | KEYWORD:fija IDENTIFIER EQ expr
                | KEYWORD:entrega expr
                | if
                | while
                | for
                | define
                | comp (KEYWORD:y | KEYWORD:o comp)*

comp            : NOT comp
                | math ((EE | NE | LT | GT | LTE | GTE) math)*

math            : term ((PLUS | MINUS) term)*

term            : factor ((MUL | DIV | MOD | FLOORDIV) factor)*

factor          : (PLUS | MINUS) factor
                | power

power           : call (POW factor)*

call            : atom (LPAREN (expr (COMMA expr)*)? RPAREN)?

atom            : INT | FLOAT | IDENTIFIER | STRING
                | LPAREN expr RPAREN
                | list

list            : LSQUARE (expr (COMMA expr)*)? RSQUARE

define          : KEYWORD:define IDENTIFIER?
                  LPAREN (IDENTIFIER (COMMA IDENTIFIER)*)? RPAREN
                  block

if              : KEYWORD:si expr block (elif)* (else)?

elif            : KEYWORD:quizas expr block

else            : KEYWORD:sino block

while           : KEYWORD:mientras expr block

for             : KEYWORD:para IDENTIFIER EQ expr
                  KEYWORD:hasta expr (KEYWORD:paso expr)?
                  block
